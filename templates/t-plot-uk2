set terminal pngcairo dashed font "Helvetica,13" size 800,600
set output "../graphs/covid-uk-linear.png"
#set logscale y
set key at -12,3700 box
#set key at -22,100
set yrange [1:4000]
set xrange [-40:0]
#set yrange [3:100]
#set xrange [-3:-15]
set ylabel "Confirmed COVID19 cases per million inhabitants"
set xlabel "Days (from DATE March 2020)"
offset = (OFFSET + 5.5)
ukpop = 67.7
frpop = 65.2
depop = 83.7
itpop = 60.5
supop = 8.6
espop = 46.7
nopop = 5.4
lompop = 10
atpop = 9.0
sepop = 9.1
lonpop = 8.9
nwpop = 7.2
epop = 6.2
swpop = 5.6
nepop = 8.1
midpop = 10.7

lonoff = 19
seoff = 23
midoff = 23
eoff = 24
swoff = 25
neoff = 24
nwoff = 23
set grid
plot "../country_data/lombardy" using ($0 +10.5 - offset):($2/lompop) w lp ps 0.5 lw 2 lc "black" t "Lombardy Region, Italy",\
"../country_data/italy" using ($0 +5 - offset):($2/itpop) w l lc "#a0a0a0" lw 3 t "All of Italy", \
"../country_data/uk-regions-cum" using ($0 + 13.5 - lonoff - offset):($3/lonpop) w l lw 3 lc "red" t sprintf("London, UK (%d days behind Lombardy)", lonoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - seoff - offset):($7/sepop) w l lw 3 lc "#00a0ff" t sprintf("South East, UK (%d days behind Lombardy)", seoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - midoff - offset):($4/midpop) w l lw 3 lc "#00a000" t sprintf("Midlands, UK (%d days behind Lombardy)", midoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - neoff - offset):($5/nepop) w l lw 3 lc "blue" t sprintf("North East, UK (%d days behind Lombardy)", neoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - nwoff - offset):($6/nwpop) w l lw 3 lc "brown" t sprintf("North West (%d days behind Lombardy)", nwoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - eoff - offset):($2/epop) w l lw 3 lc "orange" t sprintf("UK, East (%d days behind Lombardy)", eoff), \
"../country_data/uk-regions-cum" using ($0 +13.5 - swoff - offset):($8/swpop) w l lw 3 lc "magenta" t sprintf("South West, UK, (%d days behind Lombardy)", swoff), \
1.35**(x+offset - 4) lc "black"  dt 4 t "35% daily increase",\
1.22**(x+offset + 2) lc "black" dashtype 2 t "22% daily increase"

#"switzerland" using ($0+3.5-offset):($2/supop) w lp ps 0.5 lw 3 lc "brown" t "Switzerland (11 days behind Lombardy, see comment)", \
#"austria" using ($0 +4.5 - offset):($2/atpop) w lp ps 0.5 lw 3 lc "orange" t "Austria (14 days behind Lombardy)", \

