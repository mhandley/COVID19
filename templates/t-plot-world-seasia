set terminal pngcairo dashed font "Helvetica,13" size 800,600
set output "../graphs/covid-world-seasia.png"
set logscale y
set mxtics
set grid mxtics xtics ytics mytics
set key at -1,7 box
set yrange [0.5:1500]
set xrange [-45:]
set ylabel "COVID-19 cases per million inhabitants"
set xlabel "Days (from DATE March 2020)"
offset = OFFSET
jhuoffset = (OFFSET + 24)
vnoff = 37
twoff = 28
phoff = 27.5
khoff = 27
thoff = 25
plot "../country_data/italy" using ($0 +5 - offset):($2/itpop) w l lw 2 t "Italy", \
"../country_data/thailand" using ($0 -24 -thoff - offset):($2/thpop) w l lw 2 lc "#a0a000" t sprintf("Thailand (%d days behind Italy)", thoff), \
"../jhu-data/taiwan-jhu" using ($0 -twoff - jhuoffset):($2/twpop) w l lw 2 lc "orange" t sprintf("Taiwan (%d days behind Italy)", twoff), \
"../wiki-data/philippines-wiki" using ($0 +39 -phoff - jhuoffset):($2/phpop) w l lw 2 lc "blue" t sprintf("Philippines (%d days behind Italy)", phoff), \
"../wiki-data/vietnam-wiki" using ($0 +23 -vnoff - jhuoffset):($2/vnpop) w l lw 2 lc "red" t sprintf("Vietnam (%d days behind Italy)", vnoff), \
"../jhu-data/cambodia-jhu" using ($0 -khoff - jhuoffset):($2/khpop) w l lw 2 lc "#00b000" t sprintf("Cambodia (%d days behind Italy)", khoff), \
1.35**(x+offset -4) lc "black"  dt 4 t "35% daily increase", \
1.22**(x+offset + 2) lc "black" dashtype 2 t "22% daily increase"

