set terminal pngcairo dashed font "Helvetica,13" size 800,600
set output "../graphs/covid-world-norm2.png"
set logscale y
set mxtics
set grid mxtics xtics ytics mytics
set label "Updates at http://nrg.cs.ucl.ac.uk/mjh/covid19" at graph 0.02,0.98 font "Helvetica,11" textcolor "#a0a0a0"
set key at 0,20 box
set yrange [2:YMAX]
set xrange [-40:3]
set ylabel "COVID-19 cases per million inhabitants"
set xlabel "Days (from DATE March 2020)"
offset = OFFSET
usoff = 17
saoff = 27
nzoff = 22.5
ruoff = 30.5
auoff = 18.5
ukoff = 15.5
kroff = 4
caoff = 18.5
plot "../aligned/country_data/italy" using ($1):($2/itpop) w l lw 2 t "Italy", \
"../aligned/country_data/us" using ($1  - usoff):($2/uspop) w l lw 2 lc "orange" t sprintf("USA (%d days behind Italy)", usoff), \
"../aligned/country_data/uk" using ($1 - ukoff):($2/ukpop) w l lw 2 lc "blue" t sprintf("UK (%d days behind Italy)", ukoff), \
"../aligned/country_data/canada" using ($1 - caoff):($2/capop) w l lw 2 lc "red" t sprintf("Canada (%d days behind Italy)", caoff), \
"../aligned/country_data/australia" using ($1 -auoff):($2/aupop) w l lw 2 lc "#00b0b0" t sprintf("Australia (%d days behind Italy)", auoff),\
"../aligned/country_data/newzealand" using ($1 -nzoff):($2/nzpop) w l lw 2 lc "black" t sprintf("New Zealand (%d days behind Italy)", nzoff), \
1.35**(x+offset -4) lc "black"  dt 4 t "35% daily increase", \
1.22**(x+offset + 2) lc "black" dashtype 2 t "22% daily increase"

#"../aligned/country_data/southkorea" using ($1 + kroff):($2/krpop) w l lw 2  lc 6 t sprintf("S Korea (%d days ahead of Italy)", kroff), \
#"../country_data/chile" using ($0 -2.5 - offset):($2/clpop) w l lw 2 lc "blue" t "Chile (19.5 days behind Italy)", \

